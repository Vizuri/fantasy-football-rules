package com.vizuri.fantasy.domain;

import java.math.BigDecimal;

rule "CreateWeeklyStatisticHolder"
	when
		$playStatistic : PlayStatistic( $week : week, $year : year, $playerId : playerId, $statisticType : statisticType)
		not PlayerWeeklyStatistic ( week == $week, year == $year, playerId == $playerId, statisticType == $statisticType )
	then
		PlayerWeeklyStatistic weeklyStat = new PlayerWeeklyStatistic();
		weeklyStat.setWeek( $week );
		weeklyStat.setYear( $year );
		weeklyStat.setPlayerId( $playerId );
		weeklyStat.setStatisticType( $statisticType );
		insert( weeklyStat );
		System.err.println("Created weekly stat: " + weeklyStat.toString() );
end

rule "AddStatToWeeklyHolder"
	when
		$playStatistic : PlayStatistic( $week : week, $year : year, $playerId : playerId, $statisticType : statisticType)
		$weeklyStat : PlayerWeeklyStatistic ( week == $week, year == $year, playerId == $playerId, statisticType == $statisticType )
	then
		$weeklyStat.addStatisticValue( $playStatistic.statisticValue );
		System.err.println("Updated weekly stat: " + $weeklyStat.toString() );
end

rule "AccumulateWeeklyScore"
	when
		$playScore : PlayScore( $week : week, $year : year, $playerId : playerId, $rulesetId : rulesetId )
		not PlayerWeeklyScore( week == $week, year == $year, playerId == $playerId, rulesetId == $rulesetId )
		accumulate ( $score : PlayScore( week == $week, year == $year, playerId == $playerId, rulesetId == $rulesetId), $sum : sum ( $score.getScore() ))
	then
		PlayerWeeklyScore weeklyScore = new PlayerWeeklyScore();
		weeklyScore.setWeek($week);
		weeklyScore.setYear($year);
		weeklyScore.setPlayerId($playerId);
		weeklyScore.setRulesetId($rulesetId);
		weeklyScore.setCumulativeScore( new BigDecimal($sum.toString()));
		insert(weeklyScore);
		System.err.println("Created weekly score: " + weeklyScore.toString() );
end


