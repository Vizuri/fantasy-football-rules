package com.vizuri.fantasy.domain;

import java.math.BigDecimal;

rule "ResetWeeklyStatisticHolder"
	dialect "mvel"
	salience 100
	when
		$playStatistic : PlayStatistic( $week : week, $year : year, $playerId : playerId, $statisticType : statisticType, isNew == true)
		$weeklyStat : PlayerWeeklyStatistic ( week == $week, year == $year, playerId == $playerId, statisticType == $statisticType )
	then
		System.err.println("Retracting weekly stat: " + $weeklyStat );
		retract( $weeklyStat );
		$playStatistic.isNew = false;
end

rule "AccumulateWeeklyStatistic"
	salience 90
	when
		$playStatistic : PlayStatistic( $week : week, $year : year, $playerId : playerId, $statisticType : statisticType)
		not PlayerWeeklyStatistic ( week == $week, year == $year, playerId == $playerId, statisticType == $statisticType )
		accumulate ( $stat : PlayStatistic( week == $week, year == $year, playerId == $playerId, statisticType == $statisticType ), 
					$sum : sum( $stat.getStatisticValue() ) )
	then
		PlayerWeeklyStatistic weeklyStat = new PlayerWeeklyStatistic();
		weeklyStat.setWeek( $week );
		weeklyStat.setYear( $year );
		weeklyStat.setPlayerId( $playerId );
		weeklyStat.setStatisticType( $statisticType );
		weeklyStat.setStatisticValue( new BigDecimal($sum.toString()) );
		insert( weeklyStat );
		System.err.println("Accumulated weekly stat: " + weeklyStat.toString() );
end

rule "CreateWeeklyScoreHolder"
	dialect "mvel"
	when
		$playScore : PlayScore( $week : week, $year : year, $playerId : playerId, $rulesetId : rulesetId, isNew == true )
		$weeklyScore : PlayerWeeklyScore( week == $week, year == $year, playerId == $playerId, rulesetId == $rulesetId )
	then
		System.err.println("Retracted weekly score : " + $weeklyScore );
		retract( $weeklyScore );
		$playScore.isNew = false;
end

rule "AccumulateWeeklyScore"
	when
		$playScore : PlayScore( $week : week, $year : year, $playerId : playerId, $rulesetId : rulesetId, isNew == false )
		not PlayerWeeklyScore( week == $week, year == $year, playerId == $playerId, rulesetId == $rulesetId )
		accumulate ( $score : PlayScore( week == $week, year == $year, playerId == $playerId, rulesetId == $rulesetId), $sum : sum ( $score.getScore() ))
	then
		$playScore.setIsNew(false);
		PlayerWeeklyScore weeklyScore = new PlayerWeeklyScore();
		weeklyScore.setWeek($week);
		weeklyScore.setYear($year);
		weeklyScore.setPlayerId($playerId);
		weeklyScore.setRulesetId($rulesetId);
		weeklyScore.setCumulativeScore( new BigDecimal($sum.toString()));
		insert(weeklyScore);
		System.err.println("Accumulated weekly score: " + weeklyScore.toString() );
end