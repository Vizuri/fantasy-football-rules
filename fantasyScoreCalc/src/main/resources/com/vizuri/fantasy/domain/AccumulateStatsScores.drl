package com.vizuri.fantasy.domain;

import java.math.BigDecimal;

rule "ActivateDefaultRuleSet"
	dialect "mvel"
	salience 105
	when
		not RuleSet ( )
	then
		RuleSet $ruleSet = new RuleSet();
		insert( $ruleSet );
end

rule "ResetWeeklyStatisticHolder"
	dialect "mvel"
	salience 100
	when
		$playStatistic : PlayStatistic( $week : week, $year : year, $playerId : playerId, $statisticType : statisticType, isNew == true)
		$weeklyStat : PlayerWeeklyStatistic ( week == $week, year == $year, playerId == $playerId, statisticType == $statisticType )
	then		
		retract( $weeklyStat );
		System.err.println("Retracting weekly stat: " + $weeklyStat );
end

rule "ResetWeeklyScoreHolder"
	dialect "mvel"
	salience 100
	when
		$playStatistic : PlayStatistic( $week : week, $year : year, $playerId : playerId, isNew == true)
		$weeklyScore : PlayerWeeklyScore( week == $week, year == $year, playerId == $playerId )
	then
		retract ( $weeklyScore );
		System.err.println("Retracted weekly score: " + $weeklyScore );
end

rule "RetractCumulativeScores"
	dialect "mvel"
	salience 95
	when
		$playStatistic : PlayStatistic( $week : week, $year : year, $playerId : playerId, isNew == true)
		$playScore : PlayScore( week == $week, year == $year, playerId == $playerId, isFromCumulative == true )
	then
		retract ( $playScore );
		System.err.println("Retracted play score: " + $playScore );
		$playStatistic.isNew = false;
		update($playStatistic);
end

rule "ResetWeeklyScoreHolder2"
	dialect "mvel"
	salience 95
	when
		$playScore : PlayScore( $week : week, $year : year, $playerId : playerId, isNew == true)
		$weeklyScore : PlayerWeeklyScore( week == $week, year == $year, playerId == $playerId )
	then
		retract ( $weeklyScore );
		System.err.println("Retracted cumulative score due to new score fact: " + $playScore );
		$playScore.isNew = false;
		update($playScore);
end

rule "AccumulateWeeklyStatistic"
	salience 90
	when
		$playStatistic : PlayStatistic( $week : week, $year : year, $playerId : playerId, $statisticType : statisticType)
		not PlayerWeeklyStatistic ( week == $week, year == $year, playerId == $playerId, statisticType == $statisticType )
		accumulate ( $stat : PlayStatistic( week == $week, year == $year, playerId == $playerId, statisticType == $statisticType ), 
					$sum : sum( $stat.getStatisticValue() ) )
	then
		$playStatistic.setIsNew( false );
		update( $playStatistic );
		PlayerWeeklyStatistic weeklyStat = new PlayerWeeklyStatistic();
		weeklyStat.setWeek( $week );
		weeklyStat.setYear( $year );
		weeklyStat.setPlayerId( $playerId );
		weeklyStat.setStatisticType( $statisticType );
		weeklyStat.setStatisticValue( new BigDecimal($sum.toString()) );
		insert( weeklyStat );
		System.err.println("Accumulated weekly stat: " + weeklyStat.toString() );
end

rule "AccumulateWeeklyScore"
	salience 90
	when
		$playScore : PlayScore( $week : week, $year : year, $playerId : playerId, $rulesetId : rulesetId )
		not PlayerWeeklyScore( week == $week, year == $year, playerId == $playerId, rulesetId == $rulesetId )
		accumulate ( $score : PlayScore( week == $week, year == $year, playerId == $playerId, rulesetId == $rulesetId), $sum : sum ( $score.getScore() ))
	then
		$playScore.setIsNew(false);
		update ( $playScore );
		PlayerWeeklyScore weeklyScore = new PlayerWeeklyScore();
		weeklyScore.setWeek($week);
		weeklyScore.setYear($year);
		weeklyScore.setPlayerId($playerId);
		weeklyScore.setRulesetId($rulesetId);
		weeklyScore.setCumulativeScore( new BigDecimal($sum.toString()));
		insert(weeklyScore);
		System.err.println("Accumulated weekly score: " + weeklyScore.toString() );
end